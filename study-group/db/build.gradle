buildscript {
    dependencies {
        // コード生成用の依存モジュール定義
        classpath group: "org.flywaydb", name: "flyway-gradle-plugin", version: "${flywayVersion}"
        classpath group: 'org.postgresql', name: 'postgresql', version: "${postgresqlDriverVersion}"
    }
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1' apply false
	id 'io.spring.dependency-management' version '1.1.0'
  id 'org.flywaydb.flyway' version "${flywayVersion}"
  id 'nu.studer.jooq' version "${jooqGeneratorVersion}"
}

group = 'com.example.repository'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
  jooqGenerator "org.postgresql:postgresql:${postgresqlDriverVersion}"
  implementation "org.jooq:jooq:${jooqVersion}"
  implementation "org.jooq:jooq-meta:${jooqVersion}"
  implementation "org.jooq:jooq-codegen:${jooqVersion}"
}

def dbUrl = "jdbc:postgresql://${dbHost}:${dbPort}/${dbName}"
def jdbcDriver = "org.postgresql.Driver"
def jooqDestDir = "$buildDir/jooq-gen/"
def jooqDestPackage = 'com.example.db.jooq.gen'

flyway {
  url = "${dbUrl}"
  user = "${dbUser}"
  password = "${dbPassword}"
  cleanDisabled = false
}

jooq {
  configurations {
    main {
      generationTool {
        jdbc {
          driver = "${jdbcDriver}"
          url = "${dbUrl}"
          user = "${dbUser}"
          password = "${dbPassword}"
        }
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.postgres.PostgresDatabase'
            inputSchema = 'public'
          }

          generate {
            deprecated = false
            records = true
            fluentSetters = true
            daos = true
            immutablePojos = true
            pojosEqualsAndHashCode = true
          }

          target {
            packageName = "${jooqDestPackage}"
            directory = "${jooqDestDir}"
          }
          strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
        }
      }
    }
  }
}

tasks.named("generateJooq") {
  dependsOn(tasks.flywayMigrate)
}

clean {
    delete jooqDestDir
}